buildscript {
    ext.kotlin_version = '1.9.22'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.4.1'
    }
}


allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            project.android {
                if (namespace == null) {
                    namespace project.group
                }
                // Ensure compileSdkVersion is set for all plugins
                compileSdkVersion 34
                
                // Ensure Java 17 compatibility for all plugins
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
            }
        }
        
        // Set Kotlin JVM target to 17 for all projects
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
         
            kotlinOptions {
                jvmTarget = "17"
            }
        }
    }
}

rootProject.buildDir = '../build'
subprojects {
    // Exclude app module from custom buildDir - Flutter needs it at android/app/build
    // All other modules (plugins) use the custom buildDir
    if (project.name != 'app') {
        project.buildDir = "${rootProject.buildDir}/${project.name}"
    }
    // Let app module use default location which Flutter will handle
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}

subprojects {
    project.configurations.configureEach {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support' && !details.requested.name.contains('multidex') ) {
                details.useVersion "27.1.1"
            }
        }
    }
}